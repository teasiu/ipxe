#!ipxe

# Variables are specified in boot.ipxe.cfg

# Some menu defaults
# set menu-timeout 0 if no client-specific settings found
isset ${menu-timeout} || set menu-timeout 0
set submenu-timeout ${menu-timeout}
isset ${menu-default} || set menu-default exit

# Figure out if client is 64-bit capable
cpuid --ext 29 && set arch x64 || set arch x86
cpuid --ext 29 && set archl amd64 || set archl i386

###################### MAIN MENU ####################################

:start
menu iPXE boot menu for ${initiator-iqn}
item --gap --             ------------------------- Operating systems ------------------------------
item --key w windows      Boot Windows from iSCSI
item --key u ubuntu       Boot Ubuntu from iSCSI
item --key u ubuntulive   Boot ubuntu20.04LTS Live
item --key u centos       Boot CentOS from iSCSI
item --key u esxi         Boot ESXi from iSCSI
item --gap --             ------------------------- Tools and utilities ----------------------------
item --key d menu-diag    System tools...
item --key i menu-install Installers...
item --gap --             ------------------------- Advanced options -------------------------------
item --key c config       Configure settings
item shell                Drop to iPXE shell
item reboot               Reboot computer
item --key p pxelinux	  ChainLoad Pxelinux
item
item --key x exit         Exit iPXE and continue BIOS boot
choose --timeout ${menu-timeout} --default ${menu-default} selected || goto cancel
set menu-timeout 0
goto ${selected}

:cancel
echo You cancelled the menu, dropping you to a shell

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:exit
exit

:config
config
goto start

:back
set submenu-timeout 0
clear submenu-default
goto start

############ MAIN MENU ITEMS ############

:windows
echo Booting Windows from iSCSI for ${initiator-iqn}
set root-path iscsi:192.168.1.221::::iqn.2000-01.com.synology:g41s2pt.windows
set keep-san 1
sanboot ${root-path} || goto failed
goto start

:ubuntu
echo Booting Ubuntu from iSCSI for ${initiator-iqn}
set root-path iscsi:192.168.1.221::::iqn.2000-01.com.synology:g41s2pt.ubuntu
set keep-san 1
sanboot ${root-path} || goto failed
goto start

:ubuntulive
echo Booting ubuntu20.04 live
set server_ip 192.168.1.1
set nfs_path /mnt/sda3/ipxenetboot/ubuntu
kernel nfs://${server_ip}${nfs_path}/casper/vmlinuz || read void
initrd nfs://${server_ip}${nfs_path}/casper/initrd || read void
imgargs vmlinuz initrd=initrd root=/dev/nfs boot=casper netboot=nfs nfsroot=${server_ip}:${nfs_path} ip=dhcp locale=zh_CN splash quiet -- || read void
boot || read void

:centos
echo Booting CentOS from iSCSI for ${initiator-iqn}
set root-path ${base-iscsi}.CentOS
set keep-san 1
sanboot ${root-path} || goto failed
goto start

:esxi
echo Booting ESXi from iSCSI for iqn.2000-01.com.synology:ipxe.ESXI
set root-path iscsi:10.10.10.250:::1:iqn.2000-01.com.synology:ipxe.ESXI
set keep-san 1
sanboot ${root-path} || goto failed
goto start

:pxelinux
chain pxelinux.0

###################### SYSTEM TOOL MENU #############################

:menu-diag
menu Diagnostic tools
item memtest       Memtest86+
item win8pe         Win8PE
item win10pe         Win10PE
item slitaz4        Slitaz4.0cn
item slitaz         Slitaz
item
item --key 0x08 back Back to top menu...
iseq ${menu-default} menu-diag && isset ${submenu-default} && goto menu-diag-timed ||
choose selected && goto ${selected} || goto start
:menu-diag-timed
choose --timeout ${submenu-timeout} --default ${submenu-default} selected && goto ${selected} || goto start

:memtest
set base-url http://${nas_ip}:88/tools
initrd ${base-url}/memtest86_5.01.iso
chain memdisk iso raw || goto failed
goto start

:win8pe
set base-url nfs://192.168.1.1/mnt/sda3/ipxenetboot
initrd ${base-url}/win8pe.iso
chain memdisk iso raw || goto failed
goto start

:win10pe
set base-url nfs://192.168.1.1/mnt/sda3/ipxenetboot
initrd ${base-url}/win10pe.iso
chain memdisk iso raw || goto failed
goto start

:slitaz4
initrd nfs://192.168.1.1/mnt/sda3/ipxenetboot/slitaz4.iso || goto failed
chain memdisk iso raw || goto failed
goto start

:slitaz
kernel nfs://192.168.1.1/mnt/sda3/ipxenetboot/slitaz4/bzImage
initrd nfs://192.168.1.1/mnt/sda3/ipxenetboot/slitaz4/rootfs.gz rw root=/dev/null vga=normal autologin

############################ INSTALLER MENU #########################

:menu-install
menu Installers for ${initiator-iqn}
item winpe-install        Hook Windows iSCSI and boot WinPE for intallation
item win-install          Hook Windows iSCSI  for intallation
item ubuntu-install       Hook Ubuntu ${archl} iSCSI and install
item centos-install       Hook CentOS ${archl} iSCSI and install
item esxi-install         Hook ESXi ${archl} iSCSI and install
item
item --key 0x08 back      Back to top menu...
iseq ${menu-default} menu-install && isset ${submenu-default} && goto menu-install-timed ||
choose selected && goto ${selected} || goto start
:menu-install-timed
choose --timeout ${submenu-timeout} --default ${submenu-default} selected && goto ${selected} || goto start

:winpe-install
echo Booting Windows PE ${arch} installer for ${initiator-iqn}
echo (for installing Windows)
set netX/gateway ${iscsi-server}
set root-path ${base-iscsi}.windows
set keep-san 1
echo sanhook start...
sanhook ${root-path} || goto failed
set base-url http://192.168.1.221/ipxenetboot/win8pe
kernel ${base-url}/wimboot
initrd ${base-url}/bootmgr bootmgr
initrd ${base-url}/boot/bcd bcd
initrd ${base-url}/boot/boot.sdi boot.sdi
initrd ${base-url}/sources/boot.wim boot.wim
boot || goto failed
goto start

:win-install
echo Booting Windows ${arch} installer for ${initiator-iqn}
echo (for installing Windows)
set netX/gateway ${iscsi-server}
set root-path ${base-iscsi}.windows
set keep-san 1
echo sanhook start...
sanhook ${root-path} || goto failed
exit || goto start

:ubuntu-install
echo Starting Ubuntu 20.04 ${archl} installer for ${initiator-iqn}
sanboot http://mirrors.163.com/ubuntu/dists/focal/main/installer-amd64/current/legacy-images/netboot/mini.iso || goto failed
goto start

:centos-install
set base http://mirrors.163.com/centos/8.2.2004/BaseOS/x86_64/os
kernel ${base}/images/pxeboot/vmlinuz initrd=initrd.img repo=${base}
initrd ${base}/images/pxeboot/initrd.img
boot || goto failed
goto start

:esxi-install
echo Starting ESXi 6.7 installer for iqn.2000-01.com.synology:ipxe.ESXI
set root-path ${base-iscsi}.ubuntu
set keep-san 1
sanhook ${root-path} || goto failed
initrd ${base-url}/esxi6.5.iso
chain memdisk iso raw || goto failed
goto start
