#!ipxe
   set menu-timeout 16000
   iseq ${platform} efi && set menu-default win64 || set menu-default win32
   isset ${ip} || dhcp
   isset ${next-server} || set next-server 192.168.101.1
   isset ${nfs-server} || set nfs-server 192.168.101.66
   set boot-url tftp://${next-server}
   set net0/linktimeout 300000
   set net0/timeout 300000
   set net0/mtu 1492
   console --x 1024 --y 768
   console --picture http://${next-server}/tftp/slitaz.png || echo "Logo not found, continuing..."
 
#============== Set Menu ===============
:start
  menu iPXE Boot Menu --${platform}--${ip}

  item --gap --             -------------------------------- Slitaz Menu ---------------------------
  item slitaztftp				Slitaz-(tftp)
  item slitazhttp				Slitaz-(http)
  item slitaznfs				Slitaz-(nfs)
  item --gap --             ----------------------------------Ubuntu TOOL ------------------------------
  item ubuntu2004-live				Ubuntu 20.04 amd Desktop Live
  item ubuntu2404-live				Ubuntu 24.04 amd Desktop Live
  
  item --gap --             ----------------------------------WindowsPE TOOL ------------------------------
  
  item win11pe				Windows 11 PE
  
  item --gap --             -------------------------------- Advanced -----------------------------
  item --key h local        	[H] Boot from local drive
  item --key s shell 		[S] Drop to iPXE Shell
  item --key r reboot		[R] Reboot the Computer
  item --key x exit		[X] Exit iPXE and Continue BIOS Booting
  item --key c config    	[C] Configure settings
  item --key m msboot   	[M] Enter Windows Boot Manager Menu (BIOS)
  
  choose --timeout ${menu-timeout} --default ${menu-default} selected
  goto ${selected}
 
#============ Main Menu Options =============

:slitaztftp
 imgfree
 kernel ${boot-url}/ipxe/vmlinuz-3.16.55-slitaz64 initrd=rootfs.gz rw root=/dev/null video=-32 lang=zh_CN kmap=us autologin || goto retry
 initrd ${boot-url}/ipxe/rootfs.gz || goto retry
 boot || goto retry
 goto start

:slitazhttp
 imgfree
 kernel http://${next-server}/tftp/ipxe/vmlinuz-3.16.55-slitaz64 initrd=rootfs.gz rw root=/dev/null video=-32 lang=zh_CN kmap=us autologin || goto retry
 initrd http://${next-server}/tftp/ipxe/rootfs.gz || goto retry
 boot || goto retry
 goto start

:slitaznfs
 imgfree
 kernel nfs://${nfs-server}/volume1/nfs-server/vmlinuz-3.16.55-slitaz64 initrd=rootfs.gz rw root=/dev/null video=-32 lang=zh_CN kmap=us autologin || goto retry
 initrd nfs://${nfs-server}/volume1/nfs-server/rootfs.gz || goto retry
 boot || goto retry
 goto start
  
:ubuntu2004-live
echo Booting ubuntu20.04 live
kernel nfs://${nfs-server}/volume1/nfs-server/ubuntu2004/casper/vmlinuz || goto retry
initrd nfs://${nfs-server}/volume1/nfs-server/ubuntu2004/casper/initrd || goto retry
imgargs vmlinuz initrd=initrd root=/dev/nfs boot=casper netboot=nfs nfsroot=${nfs-server}:/volume1/nfs-server/ubuntu2004 ip=dhcp locale=zh_CN splash quiet -- || goto retry
boot || goto retry

:ubuntu2404-live
echo Booting ubuntu24.04 live
kernel nfs://${nfs-server}/volume1/nfs-server/ubuntu2404/casper/vmlinuz || goto retry
initrd nfs://${nfs-server}/volume1/nfs-server/ubuntu2404/casper/initrd || goto retry
imgargs vmlinuz initrd=initrd root=/dev/nfs boot=casper netboot=nfs nfsroot=${nfs-server}:/volume1/nfs-server/ubuntu2404 ip=dhcp locale=zh_CN splash quiet -- || goto retry
boot || goto retry

:win11pe
	imgfree
	iseq ${platform} pcbios && goto win11pe_bios || goto win11pe_efi

:win11pe_bios
	initrd http://${next-server}/tftp/ipxe/pe.iso
	chain memdisk iso raw || goto retry
	goto start

:win11pe_efi
	set pefile 11PEX64.WIM
	kernel http://${next-server}/tftp/ipxe/win11pe/BOOT/GRUB/WIMBOOT gui || goto retry
	initrd http://${next-server}/tftp/ipxe/win11pe/BOOT/BOOT.SDI boot.sdi || goto retry
	initrd http://${next-server}/tftp/ipxe/win11pe/EFI/BOOT/BOOTX64.EFI BOOTX64.EFI || goto retry
	initrd http://${next-server}/tftp/ipxe/win11pe/EFI/MICROSOFT/BOOT/BCD BCD || goto retry
	initrd http://${next-server}/tftp/ipxe/win11pe/BOOT/${pefile} boot.wim || goto retry
	initrd http://${next-server}/tftp/ipxe/win11pe/EFI/BOOT/FONTS/CHS_BOOT.TTF CHS_BOOT.TTF || goto retry
	initrd http://${next-server}/tftp/ipxe/win11pe/EFI/BOOT/FONTS/WGL4_BOOT.TTF WGL4_BOOT.TTF || goto retry
	boot || goto retry
	goto start


:retry
  imgfree
  prompt Error! press any key to retry
  goto start

:local
  sanboot --no-describe --drive 0x80
 

:shell
  echo Type 'exit' to go back to the menu.
  shell
  goto start
 
:reboot
  reboot
 
:exit
  exit
 
:config
  config
  goto start
 
:msboot 
  exit
